# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/Code

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)
osx_config_files=($ZSH/**/*.zsh.osx)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load OS X specific files
if [[ `uname` == 'Darwin' ]]
then
  for file in ${(M)osx_config_files:#*/path.zsh}
  do
    source $file
  done
fi

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

export NVM_DIR="/Users/blimmer/.nvm"
[ -s "$NVM_DIR/nvm.sh" ] && . "$NVM_DIR/nvm.sh"  # This loads nvm

### Added by the Heroku Toolbelt
export PATH="/usr/local/heroku/bin:$PATH"

eval "$(thefuck --alias)"

test -e "${HOME}/.iterm2_shell_integration.zsh" && source "${HOME}/.iterm2_shell_integration.zsh"

# added by travis gem
[ -f /Users/blimmer/.travis/travis.sh ] && source /Users/blimmer/.travis/travis.sh
